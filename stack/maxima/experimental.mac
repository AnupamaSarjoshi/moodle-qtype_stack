/*  Author Chris Sangwin
    Lougborough University
    Copyright (C) 2015 Chris Sangwin

    This program is free software: you can redistribute it or modify
    it under the terms of the GNU General Public License version two.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
    GNU General Public License for details.

    You should have received a copy of the GNU General Public License
    along with this program. If not, see <http://www.gnu.org/licenses/>. */


/* THIS IS EXPERIMENTAL CODE */
/* Currently this is under development by CJS and is not connected to the main STACK codebase */
/* It sits here because the long-term goal is to incorporate it */

/* More general random function - recurses across the structure.
   Notice the use of the dummy "protect()" function to stop further evaluation.
   E.g.
   rand_recurse((5+protect(2))*x^protect(2)+3*x+7);
   rand_recurse(sin([x,y,z]));
*/
rand_recurse(ex) := block(
    if (integerp(ex) or floatnump(ex) or matrixp(ex) or listp(ex)) then return(rand(ex)),
    if atom(ex) then return(ex),
    if op(ex)=protect then return(first(args(ex))),
    apply(op(ex), maplist(rand_recurse, args(ex)))
    );

/* Truncates a polynomial to only terms of degree "d" or less - always expands out */
poly_truncate(pa,d) := apply("+",maplist(lambda([ex],if hipow(ex,x)>d then 0 else ex), args(expand(pa))));

/****************************************************************/
/*  Inequality functions for STACK                              */
/*                                                              */
/*  Chris Sangwin, <chris@sangwin.com>                          */
/*  V0.2 July 2013                                              */
/*                                                              */
/****************************************************************/



/* Use nounand/noun or with the output of to_poly_solve.          */
noun_to_poly_solve_output(ex) := block([S],
   S:ev(to_poly_solve(ex, listofvars(s)), simp),
   /* Check the trivial cases. */
   if S=[] then return([]),
   if S=[ex] then return(ex),
   if atom(S) then return(S),
   if not(is(op(S)='%union)) then return(S),
   if length(args(S))=1 then 
          return(makenounandlist(first(args(S)))),
   apply("nounor", maplist(makenounandlist, args(S)))
)$

makenounandlist(ex) := block(
   if not(listp(ex)) then return(ex),
   if length(ex)=1 then return(first(ex)),
   apply("nounand", args(ex))
)$

/* This function removes duplicates (e.g. x>1 and 1<x end up the same.        */
/* Finally it orders the result.                                              */
ineq_rem_dups(ex) := block(
    if mapatom(ex) then return(ex),
    if safe_op(ex)="?%and" or safe_op(ex)="?%or" or safe_op(ex)="?%not" or safe_op(ex)="and" or safe_op(ex)="or"
        then return(ineq_rem_dups_h(op(ex), args(ex))),
    return(ex)
)$

/* This function removes duplicates, but only keeps the operator when the resulting list has 
   more than one element. */
ineq_rem_dups_h(a, ex) := block([S],
    S:sort(listify(setify((map(ineqorder_f, ex))))),
    if length(S)=1 then return(first(S)) else return(apply(a, S))
)$

/*******************************************************************************/
/* Using inequalities to represent real intervals                              */

/* Does the expression define a *single* interval, by means of an inequality?  */
/* E.g. 1<x and x<7                                                            */
/* We only accept linear inequalities in one variable, combined by "and"       */
/* We don't expect this to be the *simplest* form, that is later.              */ 
/* A single inequality is a half open interval.                                */
/* true is the whole real line, false is the empty set.                        */
interval_inequalityp(ex) := block(
    /* Emptyset and real line. */
    if equal(ex, true) then return(true),
    if equal(ex, false) then return(true),
    if atom(ex) then return(false),
    /* Half open intervals. */
    if linear_inequalityp(ex) then return(true),
    if not(is(safe_op(ex)="nounand") or is(safe_op(ex)="?%and")) then return(false),
    if is(length(listofvars(ex))>1) then return(false),
    return(all_listp(linear_inequalityp, args(ex)))
)$

/* Takes a real interval and returns the canonical form for this inequality    */
/* a<x and x<b                                                                 */
/* or, true, false, or a single inequality                                     */
interval_simplify(ex) := block([ex2, v, a, b],
    if not(interval_inequalityp(ex)) then return(ex),
    /* We know we have an interval inequality. */
    if equal(ex, true) then return(true),
    if equal(ex, false) then return(false),
    /* Remove redundant inequalities: might leave us with a single one. */
    ex:map(inequality_disp, ex),
    ex:ineq_rem_redundant(ex),
    if linear_inequalityp(ex) then return(inequality_disp(ex)),
    /* Now we are in business.  We have a single interval, of two inequalities. */
    v:listofvars(ex),
    if equal(v,lhs(first(ex))) then ex:second(args(ex)) and first(args(ex)),
    /* This should now be in the right form. */
    /* Check for an empty set (2 possibilities) */
    if (lhs(first(args(ex)))) > (rhs(second(args(ex)))) then return(false),
    if (lhs(first(args(ex)))) = (rhs(second(args(ex)))) and not(safe_op(first(args(ex))) = "<=" and safe_op(second(args(ex))) = "<=") then return(false), 
    ex
)$


/****************************************************************/
/*  Reporting support functions for STACK                       */
/*                                                              */
/*  Chris Sangwin, <chris@sangwin.com>                          */
/*  V0.1 January 2013                                           */
/*                                                              */
/****************************************************************/

/* Sample ways of representing a PRT in which we might have errors */

/* Evaluate a single node safely. */
node_no(prt,num,inputs) := block([res,err],
    /* Type checking */
    if not(listp(prt)) then (print("STACK exception: node_no expects its first argument to be a list."), return(false)),
    if not(integerp(num)) then (print("STACK exception: node_no expects its second argument to be an integer."), return(false)),
    if is(length(prt)<num) then (print("STACK exception: node_no expects its second argument to less than the length of the first."), return(false)),
    /* Do computation */
    res:errcatch(ev(prt[num],inputs,nouns)),
    if is([] = res) then 
       print(concat("Previous error generated by node number ", string(num), ".")),
    if is([] = res) then
       [] 
    else 
       first(res)
    );

/* Actually traverse the PRT with given inputs                  */
/* Inputs should be in the form of equations such as [ans1=x^2] */
traverse_prt(inputs) := block(
    /* Type checking */
    if not(listp(inputs)) then (print("STACK exception: traverse_prt expects its argument to be a list."), return(false)),
    if not(alllistp(equationp,inputs)) then (print("STACK exception: traverse_prt expects its argument to be a list of equations."), return(false)),
    /* Setup PRT */
    simp:false,
    PRTtests:[
        'ATAlgEquiv(ans1,x^3),
        'ATInt(ans2,[x^3,x]),
        'ATInt(ans2/0,[x^3,x])
    ],
    quiet:[false,false,false],
    nexttrue:[2,3,1],
    nextfalse:[1,1,1],
    /* Creatlist to store previously visited nodes */
    visited:makelist(false, length(PRTtests)),
    current_node:1,
    feedback:[],
    answernote:[],
    /* Actually traverse the tree */
    while not(visited[current_node]) do block([res],
      visited[current_node]:true,
      res:node_no(PRTtests,current_node,inputs),
      if not(listp(res)) then return(false),
      /* Feedback */
      if not(quiet[current_node]) then feedback:cons(res[4], feedback),
      feedback:cons(concat("[STACK-feedback:",string(current_node),"-",string(res[2]),"]"), feedback),
      /* Answernotes */
      if not(is(res[3] = "")) then answernote:cons(res[3], answernote), 
      answernote:cons(concat(string(current_node),"-",string(res[2])), answernote),
      /* Update to next node */
      if res[2] then
          current_node:nexttrue[current_node]
      else
          current_node:nextfalse[current_node]
    ),
    answernote:simplode(reverse(sublist(answernote, lambda([ex],not(is(ex=""))))), " | " ),
    feedback:simplode(reverse(sublist(feedback, lambda([ex],not(is(ex=""))))), " | " ),
    [answernote, feedback]
)$

print("[ STACK-reports started. ]")$

/****************************************************************/
/*  Unary minus functions for STACK                             */
/*                                                              */
/*  Chris Sangwin, <chris@sangwin.com>                          */
/*  V0.1 March 2014                                             */
/*                                                              */
/****************************************************************/

/* Transforms --x into x recursively in the case simp:false */
unary_minus_minus_simp(ex) := block(
  if atom(ex) then return(ex),
  if op(ex) = "-" and first(args(ex))<0 then return(ev(ex,simp)),
  if op(ex) = "-" and atom(first(args(ex))) then return(ex),
  if op(ex) = "-" and op(first(args(ex))) = "-" then return(first(args(first(args(ex))))),
  apply(op(ex), map(unary_minus_minus_simp, args(ex)) )
)$

/* Transforms --x into x recursively in the case simp:false */
unary_minus_add_distrib(ex) := block(
  if atom(ex) then return(ex),
  if op(ex) = "-" and atom(first(args(ex))) then return(ex),
  if op(ex) = "-" and op(first(args(ex))) = "+" then return(apply("+", map(lambda([ex2],-ex2), args(first(args(ex)))))),
  apply(op(ex), map(unary_minus_add_distrib, args(ex)) )
)$

/****************************************************************/
/*  Square root functions for STACK                             */
/*                                                              */
/*  Chris Sangwin, <C.J.Sangwin@ed.ac.uk>                       */
/*  V0.1 August 2015                                            */
/*                                                              */
/****************************************************************/

/* With simp:false */

/* Some examples:  
p1: (2 + sqrt (2)) * sqrt (2);
p2:distrib(p1);
p3:sqrt(a)*sqrt(b)*sqrt(b)*sqrt(b)*sqrt(a)*1*sqrt(b)+1;
*/

naivesqrt(ex):=block([al],
  if atom(ex) then return(ex),
  al:args(ex),
  if safe_op(ex)="*" then block([alp,alq],
    alp:sort(sublist(args(ex), lambda([ex2],equal(safe_op(ex2),"sqrt")))),
    alq:sublist(args(ex), lambda([ex2],not(equal(safe_op(ex2),"sqrt")))),
    al:append(naivesqrthelper(alp),alq)
    ),
  if safe_op(ex)="*" and length(al)=1 then return(naivesqrt(first(al))),
  apply(op(ex), map(naivesqrt, al))
);

naivesqrthelper(ex):=block(
  if length(ex)<2 then return(ex),
  if equal(first(ex), second(ex)) then return(append([first(args(first(ex)))], naivesqrthelper(rest(rest(ex))))),
  append([first(ex)], naivesqrthelper(rest(ex)))
);

